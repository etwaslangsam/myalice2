起動と終了

＊OnSatoriLoad
＄文「＊」の重複回避	有効、起動中
＄単語群「＊」の重複回避	有効、起動中
＄引数区切り追加	（sprintf,%c,3）
＄さくらスクリプトによるスコープ切り換え時	

＃─────────────────────────────
＃初回起動
＃─────────────────────────────
＊OnFirstBoot
＄今日のパンツ	（パンツ種類）
：\s[5]あ、（ユーザ名）帰ってきた！
：\s[105]お帰りなさい、（ユーザ名C）。
：えへへ〜、今日も遊びに来ちゃった♪
：\s[102]アリスさん、（ユーザ名C）が気に入ったんですか？
：\s[0]うん！\w9だって（ユーザ名）、やさしーんだもん！
この前デートした時も……\s[2]あ！\w9
ありすには内緒だった！
：\s[103]（ユーザ名C）……？
お巡りさんに捕まっても知りませんからね？
：\s[25]ごめーん、（ユーザ名）。
次のデートの時にはサービスしてあげるから許して？

＃─────────────────────────────
＃起動
＃─────────────────────────────
＊OnBoot
（set,今日のパンツ,（パンツ種類））（set,パンツ残数,5）（set,興奮度,0）（set,パンツ濡れ,0）（set,パンツ汚れ,0）
＞朝ちゅん	（添い寝）==１&&（現在時）>4||（現在時）<8
（set,好感度,（好感度）+1）（set,好感度B,（好感度B）+1）
＞（現在月）月（現在日）日	（アイベント年月日）!=（現在年）（現在月）（現在日）
＞Start

＊朝ちゅん
（set,辞書フォルダ,Bmode）（set,デフォルトサーフェス0,100）（set,デフォルトサーフェス1,10）（set,添い寝,）
\u\s[10]\h\c\s[106]おはようございます、（ユーザ名B）。\w9\n
……\s[101]昨夜はありがとうございました。\w6
（ユーザ名B）のおかげで、とても良い夢を見ることができました。\e

＊Start
：\s[1]お帰りなさい、ダーリン（はーと）
：\c\s[100]お帰りなさい、（ユーザ名B）
：\s[5]どう？\w5　ドキっとした？\e

＊Start
：\s[5]（ユーザ名）、おかえり〜！
：\c\s[105]お帰りなさい、（ユーザ名C）。\e

＊Start
：\s[28]ハァイ！　（ユーザ名）！\w6
調子はどう？
：\c\s[105]お疲れさまです、（ユーザ名C）\e

＊Start
\u\c\s[124]（ユーザ名C）、お帰りなさい。
：\s[26]ねぇ（ユーザ名）、今からデート行かない？\e

＊Start
：\s[27]すぅ……すぅ……
：\c\s[105]あ、お帰りなさい（ユーザ名C）\w6
\s[100]アリスさんは疲れていたのか眠ってしまいました
：\s[4]うぅ〜〜〜ん\n…………\s[29]あれ？（ユーザ名）？
：\s[105]おはようございます、アリスさん\e

＊Start
\c\s[125]すぅ……すぅ……
：\s[27]むにゃむにゃ…………
：\s[106]ん…………\s[102]あっ（ユーザ名B）！\w6　おかえりなさい！
：\s[4]んぅ〜？なぁに〜……？\w6
\s[29]あ、（ユーザ名）？\w5帰ってきてたの？\e

＊Start
\u\s[105]\h\s[28]…………っていうのはどう？
：\cはい。とても良いと思います。
：\s[5]でしょー？\w6
\s[0]あ、（ユーザ名）おかえりー。
：\s[124]お帰りなさい、（ユーザ名C）。
：ん？\w3 二人でなにを話してたかって？\w6
\s[8]知りたい？\w3 \s[26]でもごめんね、女の子だけのひ・み・つ\w2（はーと）
：\s[126]ごめんなさい、（ユーザ名C）。
こればかりは、教えてあげられません。
：\s[5]ねー？
：\s[105]はい。\e

＃─────────────────────────────
＃終了
＃─────────────────────────────
＊OnClose
（set,全タイマ解除,実行）（when、（好感度）>120、（set,好感度,120））（when、（好感度B）>120、（set,好感度B,120））
＞End

＊End
：\s[1]はやく帰ってきてね、あ・な・た（はーと）
：\c\s[100]行ってらっしゃいませ\w9\w9

＊End
\0\s[2]あれ？\w6　（ユーザ名）出かけるの？
\s[5]いってらっしゃ〜い！
\1\c\s[105]行ってらっしゃいませ。\n気をつけて下さいね。\w9\w9

＊End
：\s[5]あっ！　（ユーザ名）待って！
\s[26]アタシもう帰るから、家まで送ってって？
：\c\s[105]それではアリスさん、また今度。\w9
\s[103]（ユーザ名C）、送りオオカミになっちゃダメですからね？\w9\w9

＊End
：\s[24]え〜〜〜！\w5（ユーザ名）もう行っちゃうの〜〜〜？
：\c\s[103]（ユーザ名C）にも用事がありますから……
：\s[26]まっ、それならありすと二人で楽しくするからいいわ
：\s[105]よろしくお願いします。\w9\w9

＃─────────────────────────────
＃切り替え反応
＃─────────────────────────────
＊OnGhostChanging
（set,全タイマ解除,実行）（when、（好感度）>120、（set,好感度,120））（when、（好感度B）>120、（set,好感度B,120））
＞（Reference0）へ変更
\h\s[0]（ユーザ名）、友達の所に行くの？\w9
\s[9]ね、イケメンだったらこんど紹介してよ！\w9\w9

＊アリスへ変更
\u\s[100]\h\s[14]ちょっと待っててね？\w9

＊OnGhostChanged
（set,今日のパンツ,（パンツ種類））（set,パンツ残数,5）（set,興奮度,0）（set,パンツ濡れ,0）（set,パンツ汚れ,0）
（set,好感度,（好感度）+1）（set,好感度B,（好感度B）+1）
＞（Reference0）から変更
＞Start

＊アリスから変更
\u\s[100]\h\s[5]おまたせ！

＊恵理から変更
：\s[13]キョロキョロしてどうしたの？\w9
不審者みたいよ？

＊OnGhostCalling
＞（Reference0）を呼び出し
\s[100]（Ｒ０）さんをお呼びします。
：\s[0]だれ？\w3 （ユーザ名）のお友達？

＊OnGhostCalled
＞（Reference0）から呼び出し
＞Start

＊知依那から呼び出し
：\s[8]ねぇ〜（ユーザ名）〜\w6
早く私たちとイイことしましょ〜？
：\s[109]（ユーザ名C）……

＃──────────────────────────
＃メニュー
＃──────────────────────────
＊0つつかれ
＞MainMenu

＊MainMenu
\0\0（MenuTalk）\_q\n[half]
\![*]\q[なにか話して,PleaseTalk]\n[half]
\![*]\q[話しかける,OnCommunicate,user,アリス]\n[half]
\![*]\q[喋り頻度,TalkInterval]\n[half]
\![*]\q[名前を教える,UserNameChange]\n[half]
（when、（アリスキス）==1、\![*]\q[デートしよう,アリスとデート]）\n
\q[なんでもない,Cancel]
\_q\e

＠MenuTalk
\s[5]なーに？
\s[26]まかせて！
\s[1]しょーがないなぁ〜

＊Cancel
\h（CancelTalk）\e

＠CancelTalk
\s[24]もー！用がないならつつかないで！
\s[8]もう終わりぃ？
\s[5]もういいの？

＠めにゅー
\f[anchorfontcolor,#ff3366]\f[anchorpencolor,#ff3366]

＊1つつかれ
＞SubMenu

＊SubMenu
\u\1（MenuTalk2）\_l[,@5]
\_q\![*]\q[話しかける,OnCommunicate,user,ありす]\_l[,@3]
\![*]\q[二人きりになりたい,BMode]\_l[,@3]
\![*]\q[呼び方を変えて,UserNameChange2]\_l[,@5]
\q[なんでもない,Cancel2]
\_q\e

＠MenuTalk2
\s[105]はい、なんでしょうか？
\s[124]お任せください。
\s[101]なんですか？\w3　（ユーザ名C）。

＊Cancel2
\u（CancelTalk2）\e

＠CancelTalk2
\s[102]終わりですか？
\s[103]あの、いいんですか？
\s[104]わかりました……。

＊PleaseTalk
（）

＃──────────────────────────
＃喋り頻度
＃──────────────────────────
＊TalkInterval
\h\_q\n[half]\_a[TILevel0]（TalkIntervalFlag1）\_aおしずかに (喋らない)\n[half]
\_a[TILevel4]（TalkIntervalFlag2）\_aいっぱい　(１分)\n[half]
\_a[TILevel3]（TalkIntervalFlag3）\_aまあまあ　(３分)\n[half]
\_a[TILevel2]（TalkIntervalFlag4）\_aそこそこ　(５分)\n[half]
\_a[TILevel1]（TalkIntervalFlag5）\_aときたま　(１０分)\n
\q[なんでもない,Cancel]
\_q\e

＠TalkIntervalFlag1
（when,（喋り間隔）==０秒,[◯],[　]）

＠TalkIntervalFlag2
（when,（喋り間隔）==６０秒,[◯],[　]）

＠TalkIntervalFlag3
（when,（喋り間隔）==１８０秒,[◯],[　]）

＠TalkIntervalFlag4
（when,（喋り間隔）==３００秒,[◯],[　]）

＠TalkIntervalFlag5
（when,（喋り間隔）==６００秒,[◯],[　]）

＊TILevel0
＄喋り間隔	０秒
\u\s[106]分かりました、トークはなしですね。\h\s[24]むぅ〜〜〜！\e

＊TILevel4
＄喋り間隔	６０秒
\u\s[103]１分おきですね。\h\s[5]いーっぱい喋ればいいのね！\e

＊TILevel3
＄喋り間隔	１８０秒
\u\s[100]では、３分おきくらいで。\h\s[26]まっかして！\e

＊TILevel2
＄喋り間隔	３００秒
\u\s[105]５分おきですね。\h\s[0]ちょっとのんびり、ね。\e

＊TILevel1
＄喋り間隔	６００秒
\u\s[124]１０分おきに設定しました。\h\s[29]のんびりすぎて、\s[27]寝ちゃうかも……。\e

＃──────────────────────────
＃ユーザ名
＃──────────────────────────
＊UserNameChange
\h\s[2]呼び方？\w9　\s[26]なんて呼んで欲しいの？
\![open,inputbox,ユーザ名,-1,お兄さん]
\e

＊ユーザ名を教えてもらった
\h\s[5]これからは（ユーザ名）って呼べばいいのね！\e

＊ユーザ名を空打ちされた
\h\s[0]じゃ、今までどおりに呼ぶね。\e

＊UserNameChange2
\u\s[102]呼び方ですか？\w9　……\s[101]仕方ないですね。
\![open,inputbox,ユーザ名C,-1,お兄ちゃん]
\e

＊ユーザ名Cを教えてもらった
\u\s[124]わかりました、次からは（ユーザ名C）と呼びます。\e

＊ユーザ名Cを空打ちされた
\u\s[100]それじゃあ、今と同じに呼びますよ。\e

＃──────────────────────────
＃別モード
＃──────────────────────────
＊BMode
：\s[5]ちょっとあたし遊びに行ってくるねー！
：分かりました、気をつけてくださいね。
\h\s[-1]\u\s[-1]\w9
（set,辞書フォルダ,Bmode）（set,デフォルトサーフェス0,100）（set,デフォルトサーフェス1,10）（set,会話時サーフェス戻し,無効）
\u\s[10]\b[-1]
＞5years_afterBモード	（現在シェル）==5years_after
＞我慢できない	（興奮度）>0
\h\c\s[101]……二人きり、ですね。
（ユーザ名B）（はーと）

＊5years_afterBモード
（set,今日のパンツ,0）
＞我慢できない	（興奮度）>0
\h\c\s[201]……二人きり、ですね。
（ユーザ名B）（はーと）

＊我慢できない
：\c\s[127]（ユーザ名B）（はーと）\w6はやくはやく（はーと）

＊アリスとデート
\0\s[2]！\w6\n\n\s[5]ほんとに？\w3 やったー！\w6
\s[26]とゆーわけでぇ、ありすデートしてくるね！\n[half]
\1\s[104]はぁ……もう仕方ないですね。\w6
\s[107]（ユーザ名C）、変なことしちゃダメですからね？
\0\s[28]んじゃ行ってきまーす！\w9\s[-1]\1\s[-1]\w6
（set,辞書フォルダ,Wmode）（set,デフォルトサーフェス0,100）（set,デフォルトサーフェス1,10）（set,会話時サーフェス戻し,無効）
\1\c\b[-1]\0\c\s[32]んっふっふ〜……二人っきりだね（はーと）

＃──────────────────────────
＃バグ報告
＃──────────────────────────
＊OnWebClap
\![open,inputbox,拍手送信内容,-1,いいね！,--limit=1000]φ
\h\s[26]言いたいことがあったらどんどん言っていーわよ。

＊拍手送信内容を教えてもらった
\![execute,http-post,http://clap.webclap.com/clap.php?id=etwaslangsam,--param=message_body="アリス＠（拍手送信内容）！",--async=webclap]
＄拍手送信内容	

＊拍手送信内容を空打ちされた
\e

＊拍手送信完了
\h\s[5]ありがとっ！

＊拍手送信失敗
\h\s[1]「（R4）」？\w6\nよくわかんないけど失敗したっぽい。

＊OnExecuteHTTPComplete
＞拍手送信完了	（R1）==webclap

＊OnExecuteHTTPFailure
＞拍手送信失敗	（R1）==webclap
