＃ー────────────────────────────
＃メニュ
＃ー────────────────────────────
＊0つつかれ
＞賢者	（興奮度）>0
＞Menu

＊Menu
\h\![bind,ハート,ハート,0]（MenuTalk）\w9
\_q
\![*]\q[家に戻る,DateEnd]\n
\![*]\q[喋る間隔を変えて,TalkInterval]\n
\![*]\q[バグ報告,OnWebClap]\n
\q[なんでもない,Cancel]
\_q\e

＠MenuTalk
\s[0]どうしたの？

＊Cancel
\h（CancelTalk）\e

＠CancelTalk
\s[24]む〜っ！

＃─────────────────────────────
＃喋り頻度
＃─────────────────────────────
＊TalkInterval
\h\s[5]たっくさんお話しようね！\_q\n[half]
\![*]\_a[TILevel3]３分おき\_a\n[half]
\![*]\_a[TILevel2]５分おき\_a\n[half]
\![*]\_a[TILevel1]１０分おき\_a\n
\![*]\q[なんでもない,Cancel]
\_q\e

＊TILevel3
＄喋り間隔	１８０秒
\0\s[5]変更したよ。\e

＊TILevel2
＄喋り間隔	３００秒
\0\s[5]変更したよ。\e

＊TILevel1
＄喋り間隔	６００秒
\0\s[5]変更したよ。\e

＃─────────────────────────────
＃モード切替
＃─────────────────────────────
＊DateEnd
\0\s[24]え〜っ！？\w5 もうデートおしまーい？
……\s[2]ってホントだ、もうこんな時間！\w6\n
\s[4]ありすが待ってるから、はやく帰らないとね。\w6
\s[0]じゃ、行こっか。
\0\s[-1]\1\s[-1]\c\w9
＄辞書フォルダ	.
（set,デフォルトサーフェス0,0）（set,デフォルトサーフェス1,100）（set,会話時サーフェス戻し,有効）（set,全タイマ解除,実行）
\0\c\s[5]たっだいまー！\n[half]
\1\c\s[105]おかえりなさい。
\1\s[103]アリスさん、変なことされませんでした？
\0\s[28]だいじょーぶだいじょーぶ！
\s[26]何にもなかったよねー？\e


＃─────────────────────────────
＃バグ報告
＃─────────────────────────────
＊OnWebClap
\![open,inputbox,拍手送信内容,-1,いいね！,--limit=1000]φ
\h\s[26]バグ報告や感想、対応して欲しいコミュニケート
なんでも送ってね（はーと）\e

＊拍手送信内容を教えてもらった
\![execute,http-post,http://clap.webclap.com/clap.php?id=etwaslangsam,--param=message_body="アリス＠（拍手送信内容）",--async=webclap]
＄拍手送信内容	

＊拍手送信内容を空打ちされた
\e

＊OnExecuteHTTPComplete	（Reference1）==webclap
\h\s[28]送信かんりょ〜う！\e

＊OnExecuteHTTPFailure	（Reference1）==webclap
\h\s[4]「（R4）」で失敗だってぇ〜。\e
